<!DOCTYPE html>
<html>
    <head>
        <meta charset="UTF-8">
        <link rel="stylesheet" href="css/styles.css">
        <link rel="stylesheet" href="css/node.css">
        <title>Atlas - World Editor</title>
    </head>
    <body>
        <div id="container"></div>
        <!-- Shaders -->
        <script type="x-shader/x-vertex" id="vertexShader">
        varying vec2 vUv;
        void main() {
            vUv = uv;
            gl_Position = projectionMatrix * modelViewMatrix * vec4(position,1.0);
        }
        </script>

        <script type="x-shader/x-fragment" id="fragmentShader">
        varying vec2 vUv;
		uniform sampler2D screen;

		void main() {
			gl_FragColor = texture2D(screen, vUv);
		}
        </script>
        <script type="x-shader/x-fragment" id="fragmentShader_RTT">
        uniform vec2 resolution;
        uniform int view;
        vec3 mod289(vec3 x) {
            return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        vec2 mod289(vec2 x) {
            return x - floor(x * (1.0 / 289.0)) * 289.0;
        }

        vec3 permute(vec3 x) {
            return mod289(((x*34.0)+1.0)*x);
        }

        float snoise(vec2 v)
        {
            const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                              0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                             -0.577350269189626,  // -1.0 + 2.0 * C.x
                              0.024390243902439); // 1.0 / 41.0
            // First corner
            vec2 i  = floor(v + dot(v, C.yy) );
            vec2 x0 = v -   i + dot(i, C.xx);

            // Other corners
            vec2 i1;
            //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
            //i1.y = 1.0 - i1.x;
            i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
            // x0 = x0 - 0.0 + 0.0 * C.xx ;
            // x1 = x0 - i1 + 1.0 * C.xx ;
            // x2 = x0 - 1.0 + 2.0 * C.xx ;
            vec4 x12 = x0.xyxy + C.xxzz;
            x12.xy -= i1;

            // Permutations
            i = mod289(i); // Avoid truncation effects in permutation
            vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
            	+ i.x + vec3(0.0, i1.x, 1.0 ));

            vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
            m = m*m ;
            m = m*m ;

            // Gradients: 41 points uniformly over a line, mapped onto a diamond.
            // The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

            vec3 x = 2.0 * fract(p * C.www) - 1.0;
            vec3 h = abs(x) - 0.5;
            vec3 ox = floor(x + 0.5);
            vec3 a0 = x - ox;

            // Normalise gradients implicitly by scaling m
            // Approximation of: m *= inversesqrt( a0*a0 + h*h );
            m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

            // Compute final noise value at P
            vec3 g;
            g.x  = a0.x  * x0.x  + h.x  * x0.y;
            g.yz = a0.yz * x12.xz + h.yz * x12.yw;
            return 130.0 * dot(m, g);
        }

        float scene(vec3 p) {
            return p.y - snoise(vec2(p.x, p.z));
            /*return p.y - snoise(vec2(p.x));*/
             /*return length(p)-1.0;*/
        }

        vec3 getNormal(vec3 p) {
            float eps = 0.001;
            return normalize(vec3(
        		scene(vec3(p.x+eps,p.y,p.z))-scene(vec3(p.x-eps,p.y,p.z)),
        		scene(vec3(p.x,p.y+eps,p.z))-scene(vec3(p.x,p.y-eps,p.z)),
        		scene(vec3(p.x,p.y,p.z+eps))-scene(vec3(p.x,p.y,p.z-eps))
        	));
        }

        float raymarch(vec3 rayOrigin, vec3 rayDirection) {
            float t = 0.0;
            for(int i = 0; i < 100; ++i) {
                float d = scene(rayOrigin + rayDirection * t);
                if(d < 0.0) {
                    return t;
                }
                t += d * 0.125;
            }
            return t;
        }
        void main(void) {
            vec2 aspect = vec2(resolution.x/resolution.y, 1.0);
        	vec2 screenCoords = (2.0*gl_FragCoord.xy/resolution.xy - 1.0) * aspect;
            if (view == 1) {
            	vec3 lookAt = vec3(0.0, 0.0, 0.0);  // This is the point you look towards, or at.
            	vec3 camPos = vec3(20.0, 10.0, 20.0); // This is the point you look from, or camera you look at the scene through. Whichever way you wish to look at it.

                // Camera setup.
                vec3 forward = normalize(lookAt-camPos); // Forward vector.

                vec3 right = normalize(vec3(forward.z, 0., -forward.x )); // Right vector... or is it left? Either way, so long as the correct-facing up-vector is produced.
                vec3 up = normalize(cross(forward,right)); // Cross product the two vectors above to get the up vector.

                float FOV = 1.0;

                vec3 ro = camPos;
                vec3 rd = normalize(forward + FOV*screenCoords.x*right + FOV*screenCoords.y*up);

                float dist = raymarch(ro, rd);
                if ( dist >= 100.0 ) {
            	    gl_FragColor = vec4(vec3(0.05, 0.066, 0.07), 1.0);
            	    return;
            	}
                vec3 sp = ro + rd*dist;
                vec3 surfNormal = getNormal(sp);

                vec3 lp = vec3(15, 10, 15);
            	vec3 ld = lp-sp;

            	float len = length( ld ); // Distance from the light to the surface point.
            	ld /= len; // Normalizing the light-to-surface, aka light-direction, vector.
                float diffuse = max( 0.0, dot(surfNormal, ld) ); //The object's diffuse value, which depends on the angle that the light hits the object.
                vec3 Color = vec3(0.4, 0.6, 1.0);
                gl_FragColor = vec4(Color * diffuse, 1.0);
            } else {

            	vec3 forward = vec3(0, -1, 0);
                vec3 up = vec3(0, 0, 1);
                vec3 right = vec3(1, 0, 0);

                vec3 ro = vec3(0,10,0)  + screenCoords.x*right + screenCoords.y*up;
                vec3 rd = forward;

                float dist = raymarch(ro, rd);
                vec3 P = ro + rd * dist;
                P.y += 1.0;
                P.y /= 2.0;
                float f  = fract (P.y * 20.0);
                float df = fwidth(P.y * 25.0);

                float g = smoothstep(df * 0.5, df * 1.0, f);

                float c = g;
                gl_FragColor = vec4(vec3(0.4, 0.6, 1.0) * c * floor(P.y * 20.0) / 20.0, 1.0);
            }
        }
        </script>
        <script src="https://rawgit.com/mrdoob/stats.js/master/build/stats.min.js"></script>
        <script src="bundle.js"></script>
    </body>
</html>
